.table person
.table father
.table mother

grandfather{grandchild: A, grandfather: C} :-
  parent{child: A, parent: B} &
  father{child: B, father: C}.
grandmother{grandchild: A, grandmother: C} :-
  parent{child: A, parent: B} &
  mother{child: B, mother: C}.
grandparent{grandchild: A, grandparent: C} :-
  parent{child: A, parent: B} &
  parent{child: B, parent: C}.
parent{child: C, parent: P} :-
  mother{child: C, mother: P} | father{child: C, father: P}.
parents{child: C, mother: M, father: F} :-
  mother{child: C, mother: M} &
  father{child: C, father: F}.
sibling{left: L, right: R} :-
  parents{child: L, mother: M, father: F} &
  parents{child: R, mother: M, father: F} &
  L != R.
cousin{left: L, right: R} :-
  parent{child: L, parent: P1} &
  sibling{left: P1, right: P2} &
  parent{child: R, parent: P2}.
uncle{child: C, uncle: U} :-
  parent{child: C, parent: P} &
  brother{person: P, brother: U}.
aunt{child: C, aunt: U} :-
  parent{child: C, parent: P} &
  sister{person: P, sister: U}.
brother{person: L, brother: R} :-
  person{name: R, gender: "Male"} &
  sibling{left: L, right: R}.
sister{person: L, sister: R} :-
  person{name: R, gender: "Female"} &
  sibling{left: L, right: R}.
ancestor{desc: X, an: Z} :-
  parent{child: X, parent: Z} |
  parent{child: X, parent: Y} & ancestor{desc: Y, an: Z}.

person{name: "Paul", gender: "Male"}.
person{name: "Ann", gender: "Female"}.
father{child: "Pete", father: "Paul"}.
father{child: "Paul", father: "Peter"}.
father{child: "Ann", father: "Peter"}.
father{child: "Mary", father: "Mark"}.
father{child: "Bob", father: "John"}.
father{child: "Carolyn", father: "Paul"}.
father{child: "Henry", father: "Bob"}.
father{child: "Charlie", father: "Bob"}.

mother{child: "Pete", mother: "Mary"}.
mother{child: "Paul", mother: "Judith"}.
mother{child: "Ann", mother: "Judith"}.
mother{child: "Bob", mother: "Ann"}.
mother{child: "Mary", mother: "Carolyn K"}.
mother{child: "Carolyn", mother: "Mary"}.
mother{child: "Henry", mother: "Becki"}.
mother{child: "Charlie", mother: "Becki"}.

familyViz.node{id: ID} :-
  mother{child: ID} |
  mother{mother: ID} |
  father{child: ID} |
  father{father: ID}.

familyViz.edge{from: F, to: T, label: ""} :-
  mother{child: F, mother: T} |
  father{child: F, father: T}.

internal.visualization{
  name: "Family",
  spec: graphviz{
    nodes: "familyViz.node{id: ID}",
    edges: "familyViz.edge{from: From, to: To, label: Label}"
  }
}.
