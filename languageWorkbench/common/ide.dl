.table ide.Cursor
.table astInternal.firstChild
.table astInternal.lastChild
.table astInternal.next
.table internal.visualization

# === scope / reference graph ===

# TODO: decide on defnSpan vs defnSpan

scope.Item{
  scopeID: I,
  defnScopeID: DS,
  name: N,
  defnSpan: S,
  kind: K,
  type: T
} :-
  scope.defnHere{
    scopeID: I, defnScopeID: DS, name: N, defnSpan: S, kind: K, type: T
  } |
  scope.defnParent{
    scopeID: I, defnScopeID: DS, name: N, defnSpan: S, kind: K, type: T
  }.
scope.defnHere{scopeID: I, defnScopeID: I, name: N, defnSpan: S, kind: K, type: T} :-
  scope.Defn{scopeID: I, name: N, span: S, kind: K, type: T}.
scope.defnParent{scopeID: I, defnScopeID: DS, name: N, defnSpan: S, kind: K, type: T} :-
  scope.Parent{childID: I, parentID: P} &
  scope.Item{scopeID: P, defnScopeID: DS, name: N, defnSpan: S, kind: K, type: T}.

scope.Usage{
  name: N,
  defnSpan: DL,
  usageSpan: UL,
  definitionScopeID: DS,
  usageScopeID: US,
  kind: K
} :-
  scope.Item{scopeID: US, defnSpan: DL, name: N, defnScopeID: DS, kind: K} &
  scope.Var{scopeID: US, span: UL, name: N, kind: K} &
  DL != UL.

# === Rename ===

ide.RenameSpan{span: S} :-
  ide.CurrentUsageOrDefn{span: S} &
  S != "builtin".

# === Jump to Definition ===

ide.DefnForCursor{defnSpan: DS} :-
  ide.Cursor{idx: CIdx} &
  scope.Var{span: span{from: FIdx, to: TIdx}, name: N, scopeID: Scope} &
  FIdx <= CIdx & CIdx <= TIdx &
  scope.Usage{name: N, usageScopeID: Scope, defnSpan: DS}.

ide.UsageForCursor{usageSpan: US} :-
  ide.Cursor{idx: CIdx} &
  scope.Defn{span: span{from: FIdx, to: TIdx}, name: N, scopeID: Scope} &
  FIdx <= CIdx & CIdx <= TIdx &
  scope.Usage{name: N, definitionScopeID: Scope, usageSpan: US}.

# === Suggestions ===

ide.Suggestion{id: I, name: N, span: S, type: T} :-
  scope.Placeholder{scopeID: I, span: S, kind: K} &
  scope.Item{scopeID: I, name: N, kind: K, type: T}.

ide.CurrentPlaceholder{id: I, span: span{from: FIdx, to: TIdx}} :-
  scope.Placeholder{scopeID: I, span: span{from: FIdx, to: TIdx}} &
  ide.Cursor{idx: CIdx} &
  FIdx <= CIdx & CIdx <= TIdx.

ide.CurrentSuggestion{id: I, name: N, span: S, type: T} :-
  ide.CurrentPlaceholder{id: I} &
  ide.Suggestion{id: I, name: N, span: S, type: T}.

ide.CurrentProblem{desc: D, span: span{from: F, to: T}} :-
  tc.Problem{desc: D, span: span{from: F, to: T}} &
  ide.Cursor{idx: CIdx} &
  F <= CIdx & CIdx <= T.

# === Highlight ===
# TODO: consolidate a bit?

ide.CurrentVar{scopeID: UsageScope, name: N, span: span{from: F, to: T}, kind: K} :-
  scope.Var{scopeID: UsageScope, name: N, span: span{from: F, to: T}, kind: K} &
  ide.Cursor{idx: CIdx} &
  F <= CIdx & CIdx <= T.

ide.CurrentDefnOrDefnOfCurrentVar{type: "defn", span: S} :-
  ide.currentDefn{span: S} |
  ide.defnOfCurrentVar{span: S}.
ide.currentDefn{scopeID: DefnScope, name: N, span: span{from: F, to: T}} :-
  scope.Defn{scopeID: DefnScope, name: N, span: span{from: F, to: T}} &
  ide.Cursor{idx: CIdx} &
  F <= CIdx & CIdx <= T.
ide.defnOfCurrentVar{span: S} :-
  ide.CurrentVar{scopeID: UsageScope, name: N, kind: K} &
  scope.Item{scopeID: UsageScope, name: N, kind: K, defnSpan: S}.

ide.CurrentVarOrUsageOfCurrentDefn{type: "usage", span: S} :-
  ide.instanceOfCurrentVar{span: S} |
  ide.usageOfCurrentDefn{span: S}.
ide.instanceOfCurrentVar{span: S} :-
  ide.CurrentVar{scopeID: CurrentVarScope, name: N, span: CVS, kind: K} &
  scope.Item{scopeID: CurrentVarScope, defnScopeID: DefnScope, name: N, kind: K} &
  scope.Item{scopeID: OtherVarScope, defnScopeID: DefnScope, name: N, kind: K} &
  scope.Var{scopeID: OtherVarScope, name: N, kind: K, span: S}.
ide.usageOfCurrentDefn{span: UsageSpan} :-
  ide.currentDefn{scopeID: DefnScope, name: N, span: DefnSpan} &
  scope.Item{scopeID: UsageScope, defnScopeID: DefnScope, name: N, kind: K, defnSpan: DefnSpan} &
  scope.Var{scopeID: UsageScope, name: N, kind: K, span: UsageSpan}.

ide.CurrentUsageOrDefn{type: T, span: S} :-
  ide.CurrentDefnOrDefnOfCurrentVar{type: T, span: S} |
  ide.CurrentVarOrUsageOfCurrentDefn{type: T, span: S}.

# === Errors ===

tc.undefVarError{span: S, desc: undefinedVar{name: N}} :-
  scope.Var{scopeID: I, name: N, span: S} &
  !scope.Item{scopeID: I, name: N}.
