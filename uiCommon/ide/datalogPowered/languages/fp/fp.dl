# === scope ===

scope.Defn{scopeID: I, name: N, type: T, span: S} :-
  scope.builtin{id: I, name: N, type: T, span: S} |
  scope.let{id: I, name: N, type: T, span: S} |
  scope.lambda{id: I, name: N, type: T, span: S}.

scope.builtin{id: I, name: N, type: T, span: "builtin"} :-
  ast.RootExpr{id: I} &
  lang.Builtin{name: N, type: T}.
scope.let{id: Body, name: N, type: T, span: S} :-
  ast.letBodyExpr{letID: Let, bodyID: Body} &
  ast.letBindingExpr{letID: Let, bindingID: Binding} &
  ast.letVar{letID: Let, name: N, span: S} &
  tc.Type{id: Binding, type: T}.
scope.lambda{id: I, name: N, type: T, span: S} :-
  ast.lambda{id: Lambda} &
  ast.lambdaParam{id: Param, parentID: Lambda} &
  ast.type{parentID: Param, text: T} &
  ast.ident{parentID: Param, text: N, span: S} &
  ast.lambdaBody{lambdaID: Lambda, bodyID: I}.

scope.Var{scopeID: I, name: N, span: S} :-
  ast.expr{id: I} &
  ast.varExpr{parentID: I, text: N, span: S} |
  ast.expr{id: I} &
  ast.funcCall{id: FuncCallID, parentID: I} &
  ast.varExpr{parentID: FuncCallID, text: N, span: S}.

scope.Scope{id: I, label: I} :-
  ast.expr{id: I}.

scope.Placeholder{scopeID: I, span: S} :-
  ast.expr{id: I} &
  ast.placeholder{parentID: I, span: S}.

# === parent ===

scope.Parent{childID: I, parentID: P} :-
  ast.letExpr{id: L, parentID: P} &
  ast.letBindingExpr{letID: L, bindingID: I} |
  ast.letExpr{id: L, parentID: P} &
  ast.letBodyExpr{letID: L, bodyID: I} |
  ast.funcCall{id: C, parentID: P} &
  ast.funcCallArg{callID: C, argID: I} |
  ast.funcCall{id: C, parentID: P} &
  ast.funcCallFunc{callID: C, funcID: I} |
  ast.lambda{id: L, parentID: P} &
  ast.expr{parentID: L, id: I}.

ast.letBindingExpr{letID: L, bindingID: D} :-
  ast.letExpr{id: L} &
  ast.inKW{id: IN, parentID: L} &
  astInternal.next{prev: WS, next: IN} &
  astInternal.next{prev: D, next: WS}.
ast.letBodyExpr{letID: L, bodyID: I} :-
  ast.letExpr{id: L} &
  ast.inKW{id: IN, parentID: L} &
  astInternal.next{prev: IN, next: WS} &
  astInternal.next{prev: WS, next: I}.
ast.letVar{letID: L, name: N, span: S} :-
  ast.letExpr{id: L} &
  ast.ident{parentID: L, text: N, span: S}.
ast.funcCallArg{callID: C, argID: I} :-
  ast.funcCall{id: C} &
  ast.expr{parentID: C, id: I}.
ast.funcCallFunc{callID: C, funcID: F} :-
  ast.funcCall{id: C} &
  ast.varExpr{id: F, parentID: C}.
ast.lambdaBody{lambdaID: L, bodyID: B} :-
  ast.lambda{id: L} &
  ast.expr{parentID: L, id: B}.

# === Type ===

tc.Type{id: I, type: T} :-
  tc.typeS{id: I, type: T} |
  tc.typeI{id: I, type: T} |
  tc.typeLambda{id: I, type: T} |
  tc.typeFC{id: I, type: T} |
  tc.typeLet{id: I, type: T} |
  tc.typeVar{id: I, type: T} |
  tc.typePlaceholder{id: I, type: T}.

tc.typeS{id: I, type: "string"} :-
  ast.expr{id: I} &
  ast.stringLit{parentID: I}.
tc.typeI{id: I, type: "int"} :-
  ast.expr{id: I} &
  ast.intLit{parentID: I}.
tc.typeFC{id: I, type: T} :-
  ast.expr{id: I} &
  ast.funcCall{parentID: I, id: FC} &
  ast.funcCallArg{callID: FC, argID: AID} &
  ast.funcCallFunc{callID: FC, funcID: FID} &
  ast.varExpr{id: FID, text: N} &
  scope.Item{scopeID: FID, name: N, type: tapp{from: F, to: T}} &
  tc.Type{id: AID, type: F}.
tc.typeLet{id: I, type: T} :-
  ast.expr{id: I} &
  ast.letExpr{id: L, parentID: I} &
  ast.letBodyExpr{letID: L, bodyID: BID} &
  tc.Type{id: BID, type: T}.
# TOOO: convert to scope.Item
tc.typeVar{id: I, type: T} :-
  ast.expr{id: I} &
  ast.varExpr{id: V, parentID: I, text: N} &
  scope.Item{scopeID: I, name: N, type: T}.
# TODO: get this to work for multiple parameters...
tc.typeLambda{id: I, type: tapp{from: F, to: R}} :-
  ast.expr{id: I} &
  ast.lambda{id: Lambda, parentID: I} &
  ast.type{parentID: Lambda, text: R} &
  ast.lambdaParam{id: Param, parentID: Lambda} &
  ast.type{parentID: Param, text: F}.
tc.typePlaceholder{id: I, type: "unknown"} :-
  ast.expr{id: I} &
  ast.Placeholder{id: P, parentID: I}.

# === problems ===

tc.Problem{desc: D, nodeID: I, span: S} :-
  tc.wrongArgType{desc: D, nodeID: I, span: S}.
# TODO: DRY this up with tc.typeFC
tc.wrongArgType{desc: argTypeMismatch{expected: RequiredArgType, got: ArgType}, nodeID: AID, span: S} :-
  ast.funcCall{parentID: I, id: FC} &
  ast.funcCallArg{callID: FC, argID: AID} &
  ast.funcCallFunc{callID: FC, funcID: FID} &
  astInternal.node{id: AID, span: S} &
  ast.varExpr{id: FID, text: N} &
  scope.Item{scopeID: FID, name: N, type: tapp{from: RequiredArgType, to: T}} &
  tc.Type{id: AID, type: ArgType} &
  RequiredArgType != ArgType.

# === roots ===

lang.Builtin{name: "plus2", type: tapp{from: "int", to: "int"}}.

ast.RootExpr{id: 1}.

# === highlight ===

hl.mapping{rule: "intLit", type: "int"}.
hl.mapping{rule: "stringLit", type: "string"}.
hl.mapping{rule: "bool", type: "bool"}.
hl.mapping{rule: "ident", type: "ident"}.
hl.mapping{rule: "letKW", type: "keyword"}.
hl.mapping{rule: "inKW", type: "keyword"}.
