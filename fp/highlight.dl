# TODO: keywords
# TODO: cursor containment in span, not just at beginning
segment{type: T, span: S, highlight: H} :-
  segment_defn{type: T, span: S, highlight: H} |
  segment_defn_hl{type: T, span: S, highlight: H} |
  segment_usage{type: T, span: S, highlight: H} |
  segment_usage_hl{type: T, span: S, highlight: H} |
  segment_str{type: T, span: S, highlight: H} |
  segment_int{type: T, span: S, highlight: H} |
  segment_keyword{type: T, span: S, highlight: H}.

var_highlight{defnLoc: DL, usageLoc: UL} :-
  var_highlight_defn{defnLoc: DL, usageLoc: UL} |
  var_highlight_usage{defnLoc: DL, usageLoc: UL} |
  var_highlight_usage_indirect{defnLoc: DL, usageLoc: UL}.
var_highlight_defn{defnLoc: span{from: pos{idx: FIdx}, to: pos{idx: TIdx}}, usageLoc: UL} :-
  usage{definitionLoc: span{from: pos{idx: FIdx}, to: pos{idx: TIdx}}, usageLoc: UL} &
  cursor{idx: CIdx} &
  FIdx <= CIdx & CIdx <= TIdx.
var_highlight_usage{defnLoc: DL, usageLoc: span{from: pos{idx: FIdx}, to: pos{idx: TIdx}}} :-
  usage{definitionLoc: DL, usageLoc: span{from: pos{idx: FIdx}, to: pos{idx: TIdx}}} &
  cursor{idx: CIdx} &
  FIdx <= CIdx & CIdx <= TIdx.
var_highlight_usage_indirect{defnLoc: DL, usageLoc: UL} :-
  var_highlight_usage{defnLoc: DL} &
  usage{definitionLoc: DL, usageLoc: UL}.

# TODO: distinguish these by allowing BinExprs in heads or something
var_defn{span: S} :-
  let_expr{varLoc: S} |
  lambda_param{location: S}.
segment_defn{type: "defn", span: S, highlight: false} :-
  var_defn{span: S} &
  usage{definitionLoc: S}.
segment_defn_hl{type: "defn", span: S, highlight: true} :-
  var_highlight{defnLoc: S} &
  var_defn{span: S}.

segment_usage{type: "usage", span: span{from: pos{idx: FIdx}, to: pos{idx: TIdx}}, highlight: false} :-
  var{location: span{from: pos{idx: FIdx}, to: pos{idx: TIdx}}}.
segment_usage_hl{type: "usage", span: S, highlight: true} :-
  var_highlight{usageLoc: S} &
  var{location: S}.

segment_str{type: "string", span: S, highlight: false} :-
  string_lit{location: S}.
segment_int{type: "int", span: S, highlight: false} :-
  int_lit{location: S}.
segment_keyword{type: "keyword", span: S, highlight: false} :-
  let_expr{inLoc: S} |
  let_expr{letLoc: S}.
